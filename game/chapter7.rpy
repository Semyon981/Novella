

define poderevyanov = Character('Подеревянов', color="#ffa601")

define student = Character('Я', color="#00baaa")

define audio.japan = "music/japan.mp3"

define rnd = renpy.random.randint(-10, 10)

label startChapter7:

    scene computer
    with fade

    "На этот раз, приключения начинаются задолго до первого занятия по предмету."
    "Проектная деятельность. Священная корова всея политеха."
    "Самое главное на проектной деятельности - не прогадать с выбором проекта"
    "И что не менее важно - успеть заползти на понравившийся проект, пока есть свободные места"
    "И желательно друзей прихватить"
    "Чтобы записаться на некоторые, проекты нужно пройти собеседование. Но это редкость."
    "В любом случае сегодня последний день записи, расспрашивать о проектах некогда, надо уже что-то выбирать, иначе это сделает рандомайзер"
    "А из доброй сотни вариантов уже осталось всего несколько"
    "Проект “Солнечная галера”. Судя по описанию, будем делать лодку, использующую для передвижения силу солнышка нашего. И разумеется гоняться с другими изобретателями."
    "Проект “Веломобиль от политеха”. Похоже нужно будет сделать веломобиль"
    "Проект “Мониторинг состояния газовых труб”. Кажется надо будет спроектировать аппаратную и программную часть системы мониторинга состояния газовых труб"
    "Проект ”Mospolyhelper”. Тут всё понятно. Большинство студентов политеха, имеющих в своём распоряжении android-смартфон, пользуются этим приложением как заменой личного кабинета студента. Приложить руку к данному творению - великая честь."
    "”Группа проектов из сферы геймдева”. На направлении ИСиТ есть профиль, связанный с геймдевом. Так что почему бы и не попробовать поделать видеоигры."
    "”Стратегический проект: предпрофессиональная образовательная среда” - Непонятно, что значит “стратегический”, но выглядит очень важно. Из описания ничего не понятно."
    "Choose your fighter!"
    "В смысле нужно наконец выбрать проект"

    menu:
        "Солнечная галера":
            call Solar from _call_Solar

        "Веломобиль от политеха":
            call Velomobil from _call_Velomobil

        "Мониторинг состояния газовых труб":
            call GasPipes from _call_GasPipes

        "Mospolyhelper":
            call Mospolyhelper from _call_Mospolyhelper

        "Группа проектов из сферы геймдева":
            call GameDev from _call_GameDev

        "Стратегический проект: предпрофессиональная образовательная среда":
            call PredProf from _call_PredProf

            
    return


label Solar:
    scene back_pd_regata
    "Ну на самом деле айтишникам надо привыкать работать на галере."
    "Несмотря на совершенно рабские условия работы на импровизированной верфи, результаты восхищают."
    "За пару месяцев на воду был спущен целый флот."
    "Все оппоненты были биты в пух и прах во всех соревнованиях."
    show petr1 with dissolve
    "Пётр:" "Горжусь!"


    return

label Velomobil:
    scene back_pd_velomobile with fade
    "Было категорически непонятно, каким образом в данном проекте айтишнику  можно применить свои знания."
    "Поэтому, пока ребята с транспортного факультета что-то там чертили и изготавливали, одна заблудшая душа подавала гайки и шуруповёрт."
    "Всё бы ничего, но после окончания производства этой тарантайки, команда разругалась, выясняя кому в пользование достанется сея карета."
    "В итоге веломобиль “Тыква-1” был отдан охране для патрулирования территории."

    return

label GasPipes:

    "Невозможно было пройти мимо актуальности данного проекта. Ведь нефтегазовая отрасль приносит немало золотишка в карман, а значит нужно максимально глубоко погрузиться в этот бизнес."
    "Навыки программиста пригодились создании программной части системы. Если точнее - в подсистеме распределения электропитания датчиков."
    "На самом деле работка показалась чересчур сложной и нудной."
    "Ну хотя бы проект был успешно сдан в срок."
    "Тоску развеяла лента новостей. Твердили о взрыве на международном газопроводе"
    "“Северное течение”. Огромный скандал, перекрёстные обвинения, миллиардные убытки."

    "Совещание с командой выявило небольшой баг в той самой подсистеме распределения электропитания. Он с большой вероятностью приводил к сгоранию датчиков и взрыву внутри трубы."
    "Было решено промолчать об этой проблеме. А у руководителя проекта явно прибавилось седых волос."
    "Как говорит японская мудрость: Путь самурая - дебага путь."

    

    return

label Mospolyhelper:
    "Проект, откровенно говоря, сложный. Над ним уже несколько команд пару лет тужатся."
    "В этот раз за половину семестра удалось в десятый раз переворотить пользовательский интерфейс, добавить журнал посещений по физ-ре и починить электронную зачётную книжку."
    "Настоящая засада ждала на промежуточной защите проекта. Члены комиссии явно ожидали более значимых успехов, о чём представитель комиссии по фамилии Подервянов и намекнул."
    scene back_pd_samurai with fade
    show samurai with dissolve
    play music japan

    poderevyanov "Медленно ползёт улитка на гору Фудзи. Ничего не обещая. Дао её совершенно. "
    poderevyanov "Вы же, товарищи, про успехи проекта только говорите."
    poderevyanov "Ваша некомпетентность в данном вопросе всем очевидна. Восемь недель вы кормите нас обещаниями!"
    poderevyanov "И я говорю вам - хватит!"
    poderevyanov "Или покажите завершенный проект, или все пойдут на пересдачу!"
    "Сильная речь дала огромный заряд мотивации."
    "Mospolyhelper был допилен в считанные недели."
    "Хотя сколько же кофеина было затрачено."

    stop music fadeout 1.0

    return

label GameDev:
   
    scene black with fade
    "Тетрис, Сталкер, Киберпанк…"
    "Шедевры славянского геймдева!"
    scene back_pd_gamedev with fade
    "Но в мире этих титанов восходит новая звезда “Погибель ящера”!"
    "Начинать производство шедевра нужно с написания сценария."
    
    $res = 0
    menu:
        "Для начала определиться с выбором имени ГГ."
        "Святослав (+0)":
            pass
        "Доброжир (+1)":
            $res += 1
        "Богдана (+2)":
            $res += 2
    
    menu:
        "Нужно придумать для ГГ конечную цель."
        "Истребление ящеров":
            pass
        "Мир во всём мире":
            $res += 1
        "Всемирное равенство":
            $res += 2
    
    menu:
        "История должна содержать главного антагониста."
        "Самый сильный ящер":
            pass
        "Злой волшебник":
            $res += 1
        "Ящерица-цундере":
            $res += 2

    menu:
        "Дальше по списку оружие, которое будет использовать ГГ."
        "Топор":
            pass
        "Двуручный меч":
            $res += 1
        "Протез с крюком":
            $res += 2

    menu:
        "Ну и куда же без доспеха…"
        "Кольчужный панцирь":
            pass
        "Лёгкая бригантина":
            $res += 1
        "Розовый кожаный жилет":
            $res += 2

    menu:
        "У ГГ должен быть дом"
        "Острог":
            pass
        "Каменный замоrк":
            $res += 1
        "Домик на дереве":
            $res += 2

    menu:
        "Надо выбрать божество, которому поклоняется ГГ."
        "Перун":
            pass
        "Зевс":
            $res += 1
        "Не будет божества":
            $res += 2

    menu:
        "Напоследок нужно решить вопрос монетизации."
        "Платно":
            pass
        "Условно бесплатно":
            $res += 1
        "Платно и с микротранзакциями":
            $res += 2

    $p = 90 - res * 14 + rnd
    $k = 10 + res * 14 + rnd


    "Вот он знаменательный день релиза! Вся команда очень взволнована."
    "И вот шедевр уже покоряет просторы steam"
    "Через некоторое время можно узнать реакцию игроков"
    "А сделать это можно на портале Metacritic"
    "Ну честно говоря оценки средненькие. Оценка пользователей: [p]; Оценка критиков: [k]"
    "Впрочем этого вполне достаточно чтобы проект засчитали"



    return

label PredProf:
    "Не проект, а загадка"
    "Почему “стратегический”?"
    "Почему “среда”, а не “пятница”?”"
    "Непонятно…"
    "И вообще именно понедельник является днём проектной деятельности."
    "Как выяснилось на первом собрании, суть проекта в продвижении политеха в довузовских учреждениях и в отлове талантливой молодёжи."
    "В качестве основного занятия предлагаются поездки в школы и выступления перед учениками, а также организация мастер-классов."
    

    menu:
        "Но можно в качестве альтернативы предложить какую-нибудь отсебятину."
        "Испытать удачу в общении со школьниками.":
            pass
        "Делать визуальную новеллу про направление ИСиТ.":
            "Идея конечно хорошая, только эту новеллу уже сделали."
            "Придётся ходить по школам."
    

    scene back_pd_school_outside with dissolve

    "Здравствуй школа!"
    "Как вообще айтишнику могли поставить задачу где-то выступать?"
    "Как вообще разговаривать с детьми?"
    "Что если класс будет представлять из себя стаю велоцирапторов в человеческом обличии?"
    "Нельзя допускать такие мысли. Дети уже ждут."

    $respect = 0
    $promotion = 0

    menu:
        "Зайти со стуком":
            $respect += -3
            $promotion += 3
            scene back_pd_school_inside with dissolve
            "Дети, похоже, совершенно не обратили внимание на появление ещё одного гуманоида в помещении."
            "Нужно привлечь их внимание."
            call PredProfStuk

        "Зайти без стука":
            "Ну такой жест точно привлёк внимание."
            scene back_pd_school_inside with dissolve
            call PredProfNoStuk
            

    
    "Какими-то чудесными и нечеловеческими усилиями удалось закрыть этот предмет."
    "А впереди, словно туча, надвигается сессия."
    hide poderevyanov
    with dissolve
    return

label PredProfStuk:
    "Дети, похоже, совершенно не обратили внимание на появление ещё одного гуманоида в помещении."
    "Нужно привлечь их внимание."

    menu:
        "Минуточку внимания, пожалуйста.":
            $respect += -3
            $promotion += 1
            "Было наивно полагать, что призыв хоть кто-то услышит."
            

            menu:
                "Расплакаться и убежать":
                    $respect += -100
                    $promotion += -100
                    call PredProfProval
                   
                "А ну ка заткнулись все!!":
                    $respect += -3
                    $promotion += 1
                    "Класс быстро пришёл в чувство."
                    call PredProfDelo
                "Если слушать не будете, на обед не успеете":
                    $respect += -3
                    $promotion += 1
                    "Класс быстро пришёл в чувство."
                    call PredProfDelo

        "Тишина в классе!!":
            "Сработало, остальные гуманоиды притихли и смотрят в правильном направлении."
            call PredProfDelo


    return


label PredProfProval:
    scene computer with fade
    "Полный провал"
    "Хотя и этого достаточно, чтобы зачёт кое-как поставили."

    return


label PredProfNoStuk:
    menu:
        "Вежливо представиться":
            $respect += 5
            $promotion += 5
        "Запомните моё имя, твари...":
            $respect += -5
            $promotion += -10
        "Салют, пацаны и девчата!":
            $respect += 5
            $promotion += 5

    
    call PredProfDelo

    return


label PredProfDelo:

    "Надо переходить к делу."

    menu:
        "Я учусь в московском политехе, сегодня расскажу вам о нём":
            $respect += -2
            $promotion += 5
        "У меня для вас презентация":
            $respect += -3
            $promotion += -1

    "По середине выступления от велоцираптора на задней парте летит ластик."

    menu:
        "Попросить прекратить":
            $respect += -5
            $promotion += 1
        "Ловко отбить в сторону":
            $respect += 5
            $promotion += 3
        "Ловко отбить обратно":
            $respect += 5
            $promotion += -5

    "После инцидента с ластиком, возникла другая проблема. Люди массово залипают в телефоны"

    menu:
        "Занимательный факт: держание телефона в руках значительно повышает риск этого телефона лишиться.":
            $respect += 2
            $promotion += -4
        "Правильно, подписываемся на наш телеграмм.":
            $respect += 5
            $promotion += 5
        "Ребят, ну хорош в телефоны пялиться":
            $respect += -3
            $promotion += 1

    "Выступление подходит к концу."
    "По ощущениям оно продолжалось вечность"


    if respect <= 0 and promotion > 0:
        "Результат выступления неутешительный: Не удалось удержать внимание детей."
        "Ну за саму попытку как-нибудь зачёт поставят."
    elif respect > 0 and promotion <= 0:
        "Результат выступления неутешительный. Теперь в этой школе политех считают за обителей зла."
        "Ну за саму попытку как-нибудь зачёт поставят"
    elif respect <= 0 and promotion <= 0:
        "Результат выступления неутешительный. Мероприятие полностью провалилось. Полностью."
        "Ну за саму попытку как-нибудь зачёт поставят."
    elif respect > 0 and promotion > 0:
        "Результат отличный! Мероприятие прошло успешно!"
        "Зачёт точно будет получен!"

    return